apply plugin: 'com.android.application'
apply plugin: 'realm-android'

def getTag(){
    def tmpTag = 'git describe --tags'.execute([], project.rootDir).text.trim()
    if(tmpTag == ""){
        return '0.1'
    }

    return tmpTag
}

def getCommitCount(){
    def tmpCommitCount = 'git rev-list --count HEAD'.execute([], project.rootDir).text.trim()

    if(tmpCommitCount == ""){
        return 100
    }

    return 100 + Integer.parseInt(tmpCommitCount)
}

def getApiKey() {
    def apiKeyFile = file('secret.properties')
    if (apiKeyFile.canRead()) {
        def Properties props = new Properties()
        props.load(new FileInputStream(apiKeyFile))
        return props['API_KEY']
    } else {
        throw new GradleException("Missing api key. Could not read from secret.properties!")
    }
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "com.ilkayaktas.fivehundredpxclient"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode getCommitCount()
        versionName getTag()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        android.defaultConfig.vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        applicationVariants.all {
            variant -> variant.buildConfigField "String", "API_KEY", "\"" + getApiKey() + "\""
        }

    }

    packagingOptions{
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    buildToolsVersion '27.0.3'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Appcompat and Constraint Layout
    implementation "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support.constraint:constraint-layout:$rootProject.constraintsLibraryVersion" // Constraints layout
    implementation "com.android.support:design:$rootProject.supportLibraryVersion" // snackbar, floating action bar
    implementation "com.android.support:cardview-v7:$rootProject.supportLibraryVersion" // Cardview

    // Test
    testImplementation "junit:junit:$rootProject.junitVersion"
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$rootProject.espressoVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs'
    })

    // Dagger
    implementation "com.google.dagger:dagger:$rootProject.daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$rootProject.daggerVersion"
    compileOnly 'javax.annotation:jsr250-api:1.0'

    // Butterknife
    implementation "com.jakewharton:butterknife:$rootProject.butterKnifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.butterKnifeVersion"

    // EventBus
    implementation "org.greenrobot:eventbus:$rootProject.eventBusVersion"

    // Jelly Toolbar
    implementation "com.github.yalantis:jellytoolbar:$rootProject.jellyToolbar"

    // Animation library
    implementation 'com.android.support:support-compat:27.1.1'
    implementation 'com.daimajia.easing:library:2.0@aar'
    implementation 'com.daimajia.androidanimations:library:2.3@aar'

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retroFitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.retroFitVersion"
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.0'

    // RXJava
    implementation "io.reactivex.rxjava2:rxandroid:$rootProject.rxandroidVersion"

    // RXJava Retrofit adapter
    implementation "com.jakewharton.retrofit:retrofit2-rxjava2-adapter:$rootProject.rxjavaAdapterVersion"

//    // Guava
//    implementation "com.google.guava:guava:$rootProject.guavaVersion"

    // Picasso
    implementation "com.squareup.picasso:picasso:$rootProject.picassoVersion"

    // PhotoView for zoom in out
    implementation "com.github.chrisbanes:PhotoView:$rootProject.photoViewVersion"

    // Greedo 500px
    implementation "com.fivehundredpx:greedo-layout:$rootProject.greedoVersion"

    // Floating Action Button
    implementation "io.github.yavski:fab-speed-dial:$rootProject.fabVersion"

    // Permission Dispatchxer
    implementation("com.github.hotchemi:permissionsdispatcher:$rootProject.permissionDispatcherVersion") {
        // if you don't use android.app.Fragment you can exclude support for them
        exclude module: "support-v7"
    }
    annotationProcessor "com.github.hotchemi:permissionsdispatcher-processor:$rootProject.permissionDispatcherVersion"

    // Spark Button
    implementation "com.github.varunest:sparkbutton:$rootProject.sparkButtonVersion"

    // Glide
    implementation "com.github.bumptech.glide:glide:$rootProject.glideVersion"
    annotationProcessor "com.github.bumptech.glide:compiler:$rootProject.glideVersion"

    // Toasty
    implementation "com.github.GrenderG:Toasty:$rootProject.toastyVersion"
}
